line = { ((!NEWLINE ~ ANY)+) ~ NEWLINE }

heading = { "="{,6} ~ " " ~ line }

word = { (LETTER | NUMBER | SEPARATOR)* }

email = { "<" ~ (!(SEPARATOR | ">") ~ ANY)* ~ ">" }

author = { word ~ (" " ~ word)? ~ (" " ~ word)? ~ (" " ~ email)? }

authors = { author ~ (" "? ~ ";" ~ " "? ~ author)* }

version = { NUMBER+ ~ ("." ~ version)? }

revision_number = { "v"? ~ version }

date = { NUMBER{4} ~ "-" ~ NUMBER{2} ~ "-" ~ NUMBER{2} }

remark = { ": " ~ line }

revision_line = {
    revision_number ~ (" "? ~ "," ~ " "? ~ date)? ~ (" "? ~ remark)?
}

document_header = { "=" ~ " " ~ line ~ (authors ~ NEWLINE? ~ revision_line?)? }

block_title = { "." ~ line }

attribute_entry_key   = { (!("=" | "]" | ",") ~ ANY)+ }
attribute_entry_value = { PUSH("\"") ~ (!"\"" ~ ANY)* ~ POP | (!("," | "]") ~ ANY)+ }

attribute_entry = {
    ((attribute_entry_key ~ "=" ~ attribute_entry_value) | attribute_entry_value) ~ ","?
}

block_attribute_list = { "[" ~ attribute_entry* ~ "]" ~ NEWLINE }

undelimited_block_content = {(!NEWLINE ~ (delimited_block | line))+}

undelimited_block = { NEWLINE+ ~ block_title? ~ block_attribute_list? ~ undelimited_block_content }

delimited_block_characters = { (("=" | "/" | "-" | "." | "*" | "+" | "_"){4, }) | "-"{2} }


delimited_block = {
    NEWLINE* ~ block_title? ~ block_attribute_list? ~ PUSH(delimited_block_characters) ~ NEWLINE ~(!(PEEK ~ NEWLINE ) ~ (NEWLINE | block | line))+ ~ POP ~ NEWLINE*
}

block = { delimited_block | undelimited_block }


document_content = { (block)* }

document = { SOI ~ (document_header)? ~ document_content ~ EOI }
