line = { ((!NEWLINE ~ ANY)+) ~ NEWLINE }

heading = { "="{,6} ~ " " ~ line }

word = { (LETTER | NUMBER | SEPARATOR)* }

email = { "<" ~ (!(SEPARATOR | ">") ~ ANY)* ~ ">" }

author = { word ~ (" " ~ word)? ~ (" " ~ word)? ~ (" " ~ email)? }

authors = { author ~ (" "? ~ ";" ~ " "? ~ author)* }

version = { NUMBER+ ~ ("." ~ version)? }

revision_number = { "v"? ~ version }

date = { NUMBER{4} ~ "-" ~ NUMBER{2} ~ "-" ~ NUMBER{2} }

remark = { ": " ~ line }

revision_line = {
    revision_number ~ (" "? ~ "," ~ " "? ~ date)? ~ (" "? ~ remark)?
}

document_header = { "=" ~ " " ~ line ~ (authors ~ NEWLINE? ~ revision_line?)? }

block_title = { "." ~ line }

undelimited_block = { block_title? ~ line* }

delimited_block_characters = { (("=" | "/" | "-" | "." | "*" | "+" | "_"){4, }) | "-"{2} }

delimited_block = {
    block_title? ~ PUSH(delimited_block_characters) ~ NEWLINE ~ (!delimited_block_characters ~ line)* ~ POP
}

block = { delimited_block | undelimited_block }

document_content = { (NEWLINE ~ block)* }

document = { SOI ~ (document_header)? ~ document_content ~ EOI }
