line = { ((!NEWLINE ~ ANY)+) ~ NEWLINE }

heading = { "="{,6} ~ " " ~ line }

word = { (LETTER | NUMBER | CONNECTOR_PUNCTUATION | "-")* }

email = { word ~ "@" ~ word }

protocol = { "http" | "https" }

host = { (!"." ~ (LETTER | NUMBER))* }

domain_name = { (!"/" ~ (LETTER | NUMBER))+ }

path = { (LETTER | NUMBER | "/" | "." | "%")* }

hostname = { (host ~ ".")? ~ (domain_name) }

query_value = { (!("&" | NEWLINE) ~ ANY)+ }

query_key = { (!("=" | "&" | NEWLINE) ~ ANY)+ }

query_param = { query_key ~ "=" ~ query_value }

anchor = { word }

/// Is Incomplete, need rule for query and anchor
url = { protocol ~ "://" ~ hostname ~ ("/" ~ path)? ~ ("?" ~ query_param*)? ~ ("#" ~ anchor)? }

author = { word ~ (" " ~ word)? ~ (" " ~ word)? ~ (" " ~ "<" ~ (email | url) ~ ">")? }

authors = { author ~ (" "? ~ ";" ~ " "? ~ author)* }

version = { NUMBER+ ~ ("." ~ version)? }

revision_number = { "v"? ~ version }

date = { NUMBER{4} ~ "-" ~ NUMBER{2} ~ "-" ~ NUMBER{2} }

remark = { ": " ~ line }

revision_line = {
    revision_number ~ (" "? ~ "," ~ " "? ~ date)? ~ (" "? ~ remark)?
}

document_header = { "=" ~ " " ~ line ~ (authors ~ NEWLINE? ~ revision_line?)? }

block_title = { "." ~ !WHITE_SPACE ~ line }

attribute_entry_key   = { (!("=" | "]" | ",") ~ ANY)+ }
attribute_entry_value = { PUSH("\"") ~ (!"\"" ~ ANY)* ~ POP | (!("," | "]") ~ ANY)+ }

attribute_entry = {
    ((attribute_entry_key ~ "=" ~ attribute_entry_value) | attribute_entry_value) ~ ","?
}

block_attribute_list = { "[" ~ attribute_entry* ~ "]" ~ NEWLINE }


undelimited_block_content = { (!NEWLINE ~ (list_block | delimited_block | line))+ }

undelimited_block = { NEWLINE+ ~ block_title? ~ block_attribute_list? ~ undelimited_block_content }

delimited_block_characters = { (("=" | "/" | "-" | "." | "*" | "+" | "_"){4, }) | "-"{2} }

delimited_block = {
    NEWLINE* ~ block_title? ~ block_attribute_list? ~ PUSH(delimited_block_characters) ~ NEWLINE ~ (!(PEEK ~ NEWLINE) ~ (NEWLINE | block | line))+ ~ POP ~ NEWLINE*
}

unordered_list_identifier = { ("*" | "+")+ }

ordered_list_identifier = { ("." | ((NUMBER | LETTER) ~ "."))+ }

list_item = { WHITE_SPACE* ~ ((unordered_list_identifier | ordered_list_identifier) ~ WHITE_SPACE+) ~ line }

// Deal with the nested lists when converting to AST, Would prefer to to that here, but can't figure out how
list_block = { NEWLINE* ~ block_title? ~ block_attribute_list? ~ list_item+ }

block = { list_block | delimited_block | undelimited_block }

section = { NEWLINE* ~ heading ~ (!heading ~ (block | line))* }

document_content = { (section | block)* }

/// Rule for the entire document, this is the entry point
document = { SOI ~ (document_header)? ~ document_content ~ EOI }
